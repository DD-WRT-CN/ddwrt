dnl Process this with autoconf to create configure
AC_INIT(lib/demangle.cc)

# This works around the fact that libtool configuration may change LD
# for this particular configuration, but some shells, instead of
# keeping the changes in LD private, export them just because LD is
# exported.
ORIGINAL_LD_FOR_MULTILIBS=$LD

dnl Default to --enable-multilib
AC_ARG_ENABLE(multilib,
[  --enable-multilib       build many library versions (default)],
[case "${enableval}" in
  yes) multilib=yes ;;
  no)  multilib=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for multilib option) ;;
 esac], [multilib=yes])dnl

dnl We may get other options which we don't document:
dnl --with-target-subdir, --with-multisrctop, --with-multisubdir

if test -n "${with_target_subdir}" && test "${srcdir}" = "."; then
  if test "${with_target_subdir}" != "."; then
    libdemangler_basedir="${with_multisrctop}../"
  else
    libdemangler_basedir="${with_multisrctop}"
  fi
else
  libdemangler_basedir=
fi
AC_SUBST(libdemangler_basedir)

AC_CANONICAL_HOST

AC_PROG_CC
libdemangler_AC_PROG_CXX
AM_PROG_LIBTOOL

AM_INIT_AUTOMAKE(demangler,1.0,no-define)

AC_EXEEXT
AM_MAINTAINER_MODE

libdemangler_AC_NEW_DEMANGLER
if test -d ${srcdir}/${libdemangler_basedir}../libiberty; then
  SUBDIRS=.
  if test $libdemangler_cv_new_demangler = yes; then
    libiberty_target=new_demangler
  else
    libiberty_target=old_demangler
  fi
else
  SUBDIRS=lib
  libiberty_target=dummy_demangler
  if test $libdemangler_cv_new_demangler != yes; then
    AC_MSG_ERROR([
*** C++ compiler is unable to compile a working demangler!])
  fi
fi
AC_SUBST(libiberty_target)
AC_SUBST(SUBDIRS)

if test -n "$with_cross_host" &&
   test x"$with_cross_host" != x"no"; then
  toolexecdir='$(exec_prefix)/$(target_alias)'
  toolexeclibdir='$(toolexecdir)/lib'
else
  toolexecdir='$(libdir)/gcc-lib/$(target_alias)'
  toolexeclibdir='$(libdir)'
fi
multi_os_directory=`$CC -print-multi-os-directory 2> /dev/null`
if test $? = 0; then
  case $multi_os_directory in
    .) ;; # Avoid trailing /.
    *) toolexeclibdir=$toolexeclibdir/$multi_os_directory ;;
  esac
fi
AC_SUBST(toolexecdir)
AC_SUBST(toolexeclibdir)

if test "${multilib}" = "yes"; then
  multilib_arg="--enable-multilib"
else
  multilib_arg=
fi

AC_OUTPUT(Makefile lib/Makefile,
[
if test -n "$CONFIG_FILES"; then
  if test -n "${with_target_subdir}"; then
    LD="${ORIGINAL_LD_FOR_MULTILIBS}"
    ac_file=Makefile . ${srcdir}/${libdemangler_basedir}../config-ml.in
  fi
fi
],
srcdir=${srcdir}
host=${host}
target=${target}
with_multisubdir=${with_multisubdir}
ac_configure_args="${multilib_arg} ${ac_configure_args}"
CONFIG_SHELL=${CONFIG_SHELL-/bin/sh}
libdemangler_basedir=${libdemangler_basedir}
CC="${CC}"
DEFS="$DEFS"
ORIGINAL_LD_FOR_MULTILIBS="${ORIGINAL_LD_FOR_MULTILIBS}"
)
